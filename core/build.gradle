dependencies {
    // API
    api project(':api')

    // Paper API
    compileOnly 'io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT'

    // Hook plugins
    compileOnly 'org.geysermc.floodgate:api:2.2.5-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT'
    compileOnly("com.github.brcdev-minecraft:shopgui-api:3.2.0") { exclude group: "*" }
    compileOnly 'com.palmergames.bukkit.towny:towny:0.102.0.6'
    compileOnly 'com.bgsoftware:SuperiorSkyblockAPI:2025.2.1'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'su.nightexpress.coinsengine:CoinsEngine:2.6.0'
    compileOnly 'com.github.Gypopo:EconomyShopGUI-API:1.9.0'
    compileOnly 'world.bentobox:bentobox:3.10.0-SNAPSHOT'
    compileOnly 'su.nightexpress.excellentshop:Core:4.22.0'
    compileOnly("io.github.fabiozumbi12.RedProtect:RedProtect-Core:8.1.2") { exclude group: "*" }
    compileOnly("io.github.fabiozumbi12.RedProtect:RedProtect-Spigot:8.1.2") { exclude group: "*" }
    compileOnly 'dev.aurelium:auraskills-api-bukkit:2.3.9'
    compileOnly 'pl.minecodes.plots:plugin-api:4.6.2'
    compileOnly("fr.maxlego08.shop:zshop-api:3.3.2")
    compileOnly("fr.maxlego08.menu:zmenu-api:1.1.0.9")

    // Implementation dependencies
    implementation 'com.github.GriefPrevention:GriefPrevention:18.0.0'
    implementation 'com.github.IncrediblePlugins:LandsAPI:7.23.0'
    implementation 'com.github.Xyness:SimpleClaimSystem:1.12.3.2'
    implementation("com.github.Zrips:Residence:6.0.0.1") { exclude group: "org.bukkit" }

    compileOnly("io.lumine:Mythic-Dist:5.11.2")
    compileOnly("com.iridium:IridiumSkyblock:4.1.2")

    implementation(platform("com.intellectualsites.bom:bom-newest:1.55"))
    compileOnly("com.intellectualsites.plotsquared:plotsquared-core")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    // BStats
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-nowarn']
}

jar {
    archiveBaseName.set("SmartSpawner")
    archiveVersion.set("${version}")

    from { project(':api').sourceSets.main.output }
    from sourceSets.main.output
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    // destinationDirectory = file('C:\\Users\\notni\\OneDrive\\Desktop\\paper_1.21.8\\plugins')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand props
    }
}
